plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'pl.spaceis.plugin'
    version = '1.4.0'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileJava.options.encoding = 'UTF-8'
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name = 'sonatype-repo'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            name = 'papermc'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
    }

    shadowJar {
        dependencies {
            exclude(dependency('org.jetbrains:annotations'))
        }

        def libsPackage = "${rootProject.group}.libs"
        relocate 'kotlin', "${libsPackage}.kotlin"
        relocate 'okhttp3', "${libsPackage}.okhttp3"
        relocate 'okio', "${libsPackage}.okio"
    }
}

dependencies {
    implementation project(':bukkit')
    implementation project(':bungee')
    implementation project(':velocity')
}

shadowJar {
     archiveFileName = "${archiveBaseName.get()} v${archiveVersion.get()}.${archiveExtension.get()}"
}
